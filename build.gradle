plugins {
  id 'groovy'
  id 'idea'
  id 'jacoco'

  id 'maven'
  id 'signing'
  id 'com.gradle.plugin-publish' version '0.9.1'
}

description = 'Gradle plugin to find duplicate code using PMDs copy/paste detection (= CPD)'
group = 'de.aaschmid'
version = '1.0'

ext {
    isBuildOnJenkins = System.getenv('BUILD_TAG')?.startsWith('jenkins-') ?: false

    integTestClasspath = [ sourceSets.main.output.resourcesDir, sourceSets.main.output.classesDir ].join(';')
    integTestWorkDir = "${rootProject.buildDir}/integTestWorkDir"
}

sourceCompatibility = '1.6'

repositories {
    mavenCentral()
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
    integTest {
        compileClasspath += configurations.provided + main.output + test.output
        runtimeClasspath += configurations.provided + main.output + test.output
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

dependencies {
    provided gradleApi()
    provided localGroovy()

    provided 'javax.inject:javax.inject:1'
    provided 'net.sourceforge.pmd:pmd-dist:5.4.2'

    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude group: 'org.codehaus.groovy'
    }

    integTestCompile 'org.hamcrest:hamcrest-core:1.3'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

javadoc {
    classpath += configurations.provided
}

jar {
    manifest {
        attributes (
            'Built-By': "Gradle ${gradle.gradleVersion}",
            'Built-Date': new Date(),
            'Specification-Title': 'gradle-cpd-plugin',
            'Specification-Version': version,
            'Specification-Vendor': 'Andreas Schmid, service@aaschmid.de',
            'Implementation-Title': 'gradle-cpd-plugin',
            'Implementation-Version': version,
            'Implementation-Vendor': 'Andreas Schmid, service@aaschmid.de',
        )
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

test {
    ignoreFailures = isBuildOnJenkins

    testLogging {
        exceptionFormat "full"
    }
}

jacoco {
    toolVersion = '0.7.2.201409121644'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

task integTest(type: Test) {
    inputs.files jar
    shouldRunAfter 'test'

    ignoreFailures = isBuildOnJenkins

    testClassesDir = sourceSets.integTest.output.classesDir
    testSrcDirs = sourceSets.integTest.java.srcDirs as List

    classpath = sourceSets.integTest.runtimeClasspath

    reports.junitXml.destination = file("${project.buildDir}/integTest-results")
    reports.html.destination = file("${project.reportsDir}/${name}")

    // System properties
    systemProperty 'integTest.gradleHomeDir', gradle.gradleHomeDir
    systemProperty 'integTest.additionalClasspath', integTestClasspath
    systemProperty 'integTest.workDir', integTestWorkDir
}
check.dependsOn(integTest)

idea {
    module {
        scopes.PROVIDED.plus += [ configurations.provided ]

        scopes.TEST.plus.add(configurations.integTestCompile)
        scopes.TEST.plus.add(configurations.integTestRuntime)

        testSourceDirs += sourceSets.integTest.groovy.srcDirs
        testSourceDirs += sourceSets.integTest.resources.srcDirs
    }

    // TODO seems to be broken with new version of IntelliJ
    workspace.iws.withXml { provider ->
        Node node = provider.asNode()

        // Add int test configuration to JUnit defaults
        def defaultTestVmParams = [
                "-DintegTest.gradleHomeDir=${gradle.gradleHomeDir}",
                "-DintegTest.additionalClasspath=${integTestClasspath}",
                "-DintegTest.workDir=${integTestWorkDir}",
            ]

        Node runManagerConfig = node.component.find { it.'@name' == 'RunManager' }
        Node runConfig = runManagerConfig.configuration.find { it.'@type' == 'JUnit'}
        Node vmParameters = runConfig.option.find { it.'@name' == 'VM_PARAMETERS' }
        vmParameters.'@value' = defaultTestVmParams.collect { it.contains(" ") ? "\"$it\"" : it }.join(" ")

        // Add int test environment variables
        def lang = System.getenv('LANG') ?: 'en_US.UTF-8'
        def envVars = runConfig.envs.first()
        envVars.children.each { child -> envVars.remove(child) }
        envVars.appendNode('env', [name: 'LANG', value: lang])
    }
}

// -- build and publish artifacts -------------------------------------------------------------------------------------

signing {
    // requires gradle.properties, see http://www.gradle.org/docs/current/userguide/signing_plugin.html
    required {
        gradle.taskGraph.hasTask('uploadArchives')
    }
    sign configurations.archives
}

if (!hasProperty('sonatypeUsername')) {
	ext.sonatypeUsername = ''
}
if (!hasProperty('sonatypePassword')) {
	ext.sonatypePassword = ''
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment ->
                signing.signPom(deployment)
            }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                // username and password from gradle.properties
                authentication(userName: sonatypeUsername, password: sonatypePassword )
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword )
            }

            pom {
                project {
                    name 'gradle-cpd-plugin'
                    packaging 'jar'
                    description project.description
                    url 'https://github.com/aaschmid/gradle-cpd-plugin'

                    scm {
                        url 'scm:git@github.com:aaschmid/gradle-cpd-plugin.git'
                        connection 'scm:git@github.com:aaschmid/gradle-cpd-plugin.git'
                        developerConnection 'scm:git@github.com:aaschmid/gradle-cpd-plugin.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'aaschmid'
                            name 'Andreas Schmid'
                        }
                    }
                }
                whenConfigured { pom ->
                    pom.dependencies.removeAll(pom.dependencies.findAll{ dep -> dep.scope in [ 'compile', 'test' ] })
                }
            }
        }
    }
}

// Note: The plugin has to be uploaded to Maven Central first
pluginBundle {
    website = 'https://github.com/aaschmid/gradle-cpd-plugin'
    vcsUrl = 'https://github.com/aaschmid/gradle-cpd-plugin'

    description = 'A Gradle plugin to find duplicate code using PMDs copy/paste detection (= CPD).'
    tags = [ 'duplicates', 'cpd', 'copy-paste-detection' ]

    plugins {
        cpdPlugin {
            id = 'de.aaschmid.cpd'
            displayName = 'Gradle CPD plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = 'gradle-cpd-plugin'
    }
}
